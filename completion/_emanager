#compdef emanager

_emanager() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :->commands' \
        '*: :->args'

    case $state in
        commands)
            local commands=(
                'use:Switch to a preset, loading its environment variables'
                'clear:Clear any active environment variables set by emanager'
                'list:List all available presets'
                'show:Show the contents of a specific preset'
                'add:Add or update a variable in a preset'
                'remove:Remove a preset'
                'config:Manage the configuration file path'
            )
            _describe 'emanager command' commands
            ;;
        args)
            case $words[2] in
                use|show|remove)
                    local presets=(${(f)"$(emanager list 2>/dev/null | grep '^- ' | sed 's/^- //')"})
                    if [[ ${#presets[@]} -gt 0 ]]; then
                        _describe 'presets' presets
                    fi
                    ;;
                config)
                    if [[ $CURRENT -eq 3 ]]; then
                        local config_commands=(
                            'set-path:Set the path to the presets JSON file'
                            'get-path:Get the current path of the presets JSON file'
                        )
                        _describe 'config commands' config_commands
                    fi
                    ;;
                add)
                    if [[ $CURRENT -eq 3 ]]; then
                        local presets=(${(f)"$(emanager list 2>/dev/null | grep '^- ' | sed 's/^- //')"})
                        if [[ ${#presets[@]} -gt 0 ]]; then
                            _describe 'presets' presets
                        fi
                    fi
                    ;;
            esac
            ;;
    esac
}
